plugins {
    id 'groovy'
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'pl.allegro.tech.build.axion-release' version '1.7.0'

}

group 'com.rundeck.plugins'
scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
version = scmVersion.version


sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

mainClassName = 'com.rundeck.plugin.aws.AWSPluginMain'
applicationName = 'rundeck-aws-cli'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'com.github.rundeck.cli-toolbelt:toolbelt:0.2.4'
    compile 'com.github.rundeck.cli-toolbelt:toolbelt-jewelcli:0.2.4'
    compile 'org.slf4j:slf4j-nop:1.7.25'

    compile group: 'software.amazon.awssdk', name: 's3', version: '2.11.10'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile "org.testcontainers:testcontainers:1.12.1"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
shadowJar {

}

task copyArtifact(type: Copy) {
    from file("$buildDir/libs/aws-s3-remote-plugin-$version-all.jar")
    into file("script-plugin/contents")
}

task removeArtifact(type: Delete) {
    delete fileTree('script-plugin') {
        include '**/*.jar'
    }
}

task plugin(type: GradleBuild) {
    buildFile = 'script-plugin/build.gradle'
    tasks = ['clean','build']
}

task cleanPlugin(type: GradleBuild) {
    buildFile = 'script-plugin/build.gradle'
    tasks = ['clean']
}

build.dependsOn copyArtifact
build.dependsOn plugin

clean.dependsOn removeArtifact
clean.dependsOn cleanPlugin
